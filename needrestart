#!/usr/bin/perl

# nagios: -epn

# needrestart - Restart daemons after library updates.
#
# Authors:
#   Thomas Liske <thomas@fiasko-nw.net>
#
# Copyright Holder:
#   2013 - 2016 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]
#
# License:
#   This program is free software; you can redistribute it and/or modify
#   it under the terms of the GNU General Public License as published by
#   the Free Software Foundation; either version 2 of the License, or
#   (at your option) any later version.
#
#   This program is distributed in the hope that it will be useful,
#   but WITHOUT ANY WARRANTY; without even the implied warranty of
#   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#   GNU General Public License for more details.
#
#   You should have received a copy of the GNU General Public License
#   along with this package; if not, write to the Free Software
#   Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301 USA
#

use Cwd qw(realpath);
use Getopt::Std;
use NeedRestart;
use NeedRestart::UI;
use NeedRestart::Interp;
use NeedRestart::Kernel;
use NeedRestart::Utils;
use Sort::Naturally;
use Locale::TextDomain 'needrestart';

use warnings;
use strict;

$|++;
$Getopt::Std::STANDARD_HELP_VERSION++;

my $LOGPREF = '[main]';
my $is_systemd = -d qq(/run/systemd/system);
my $is_tty = (-t *STDERR || -t *STDOUT || -t *STDIN);

sub HELP_MESSAGE {
    print <<USG;
Usage:

  needrestart [-vn] [-c <cfg>] [-r <mode>] [-f <fe>] [-bkl]

    -v		be more verbose
    -q		be quiet
    -m <mode>	set detail level
	e	(e)asy mode
	a	(a)dvanced mode
    -n		set default answer to 'no'
    -c <cfg>	config filename
    -r <mode>	set restart mode
	l	(l)ist only
	i	(i)nteractive restart
	a	(a)utomatically restart
    -b		enable batch mode
    -p          enable nagios plugin mode
    -f <fe>	overwrite debconf frontend

  By using the following options only the specified checks are performed:
    -k          check for obsolete kernel
    -l          check for obsolete libraries

    --help      show this help
    --version   show version information

USG
}

sub VERSION_MESSAGE {
    print <<LIC;

needrestart $NeedRestart::VERSION - Restart daemons after library updates.

Authors:
  Thomas Liske <thomas\@fiasko-nw.net>

Copyright Holder:
  2013 - 2016 (C) Thomas Liske [http://fiasko-nw.net/~thomas/]

Upstream:
  https://github.com/liske/needrestart

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 2 of the License, or
(at your option) any later version.

LIC
#/
}

our %nrconf = (
    verbosity => 1,
    hook_d => '/etc/needrestart/hook.d',
    notify_d => '/etc/needrestart/notify.d',
    sendnotify => 1,
    restart => 'i',
    defno => 0,
    ui_mode => 'a',
    systemctl_combine => 0,
    blacklist => [],
    blacklist_rc => [],
    override_rc => [],
    override_cont => [],
    interpscan => 1,
    kernelhints => 1,
);

# backup ARGV (required for Debconf)
my @argv = @ARGV;

our $opt_c = '/etc/needrestart/needrestart.conf';
our $opt_v;
our $opt_r;
our $opt_n;
our $opt_m;
our $opt_b;
our $opt_f;
our $opt_k;
our $opt_l;
our $opt_p;
our $opt_q;
unless(getopts('c:vr:nm:bf:klpq')) {
    HELP_MESSAGE;
    exit 1;
}

# disable exiting and STDOUT in Getopt::Std for further use of getopts
$Getopt::Std::STANDARD_HELP_VERSION = undef;

# restore ARGV
@ARGV = @argv;

die "ERROR: Could not read config file '$opt_c'!\n" unless(-r $opt_c || $opt_b);

# overwrite debconf frontend
$ENV{DEBIAN_FRONTEND} = $opt_f if($opt_f);

# be quiet
if($opt_q) {
    $nrconf{verbosity} = 0;
}
# be verbose
elsif($opt_v) {
    $nrconf{verbosity} = 2;
}

# slurp config file
print STDERR "$LOGPREF eval $opt_c\n" if($nrconf{verbosity} > 1);
eval do { local(@ARGV, $/) = $opt_c; <>};
die "Error parsing $opt_c: $@" if($@);

# fallback to stdio on verbose mode
$nrconf{ui} = qq(NeedRestart::UI::stdio) if($nrconf{verbosity} > 1);

die "Hook directory '$nrconf{hook_d}' is invalid!\n" unless(-d $nrconf{hook_d} || $opt_b);
$opt_r = $nrconf{restart} unless(defined($opt_r));
die "ERROR: Unknown restart option '$opt_r'!\n" unless($opt_r =~ /^(l|i|a)$/);
$is_tty++ if($opt_r eq 'i' && exists($ENV{DEBIAN_FRONTEND}) && $ENV{DEBIAN_FRONTEND} eq 'noninteractive');
$opt_r = 'l' if(!$is_tty && $opt_r eq 'i');

$opt_m = $nrconf{ui_mode} unless(defined($opt_m));
die "ERROR: Unknown UI mode '$opt_m'!\n" unless($opt_m =~ /^(e|a)$/);
$opt_r = 'l' if($opt_m eq 'e');

$nrconf{defno}++ if($opt_n);
$opt_b++ if($opt_p);

# running mode (user or root)
my $uid = $<;
if($uid) {
    if($opt_p) {
	print STDERR "UNKN - This plugin needs to be run as root!\n";
	exit 3;
    }

    print STDERR "$LOGPREF running in user-mode\n" if($nrconf{verbosity} > 1);
}
else {
    print STDERR "$LOGPREF running in root-mode\n" if($nrconf{verbosity} > 1);
}

# get current runlevel, fallback to '2'
my $runlevel = `who -r` || '';
chomp($runlevel);
$runlevel = 2 unless($runlevel =~ s/^.+run-level (\S)\s.+$/$1/);

# get UI
my $ui = ($opt_b ? NeedRestart::UI->new(0) : needrestart_ui($nrconf{verbosity}, ($is_tty ? $nrconf{ui} : 'NeedRestart::UI::stdio')));
die "Error: no UI class available!\n" unless(defined($ui));

# enable/disable checks
unless(defined($opt_k) || defined($opt_l)) {
    $opt_k = ($uid ? undef : 1);
    $opt_l = 1;
}

sub parse_lsbinit($) {
    my $rc = '/etc/init.d/'.shift;
    my %lsb;

    # ignore upstart-job magic
    if(-l $rc && readlink($rc) eq '/lib/init/upstart-job') {
	print STDERR "$LOGPREF ignoring $rc since it is a converted upstart job\n" if($nrconf{verbosity} > 1);
	return ();
    }

    open(HLSB, '<', $rc) || die "Can't open $rc: $!\n";
    my $found;
    while(my $line = <HLSB>) {
	unless($found) {
	    $found++ if($line =~ /^### BEGIN INIT INFO/);
	    next;
	}
	elsif($line =~ /^### END INIT INFO/) {
	    last;
	}

	chomp($line);
	$lsb{lc($1)} = $2 if($line =~ /^# ([^:]+):\s+(.+)$/);
    }

    unless($found) {
	print STDERR "WARNING: $rc has no LSB tags!\n" unless(%lsb);
	return ();
    }

    # pid file heuristic
    $found = 0;
    my %pidfiles;
    while(my $line = <HLSB>) {
	if($line =~ m@(\S*/run/[^/]+.pid)@ && -r $1) {
	    $pidfiles{$1}++;
	    $found++;
	}
    }
    $lsb{pidfiles} = [keys %pidfiles] if($found);
    close(HLSB);

    return %lsb;
}

print STDERR "$LOGPREF detected systemd\n" if($nrconf{verbosity} > 1 && $is_systemd);

my @systemd_restart;
sub restart_cmd($) {
    my $rc = shift;

    if($rc =~ /.+\.service$/) {
	if($nrconf{systemctl_combine}) {
	    push(@systemd_restart, $rc);
	    ();
	}
	else {
	    (qw(systemctl restart), $rc);
	}
    }
    elsif($rc eq q(systemd manager)) {
	(qw(systemctl daemon-reexec));
    }
    elsif($rc eq q(sysv init)) {
	(qw(telinit u));
    }
    else {
	if($is_systemd) {
	    if($nrconf{systemctl_combine}) {
		push(@systemd_restart, qq($rc.service));
		();
	    }
	    else {
		(qw(systemctl restart), qq($rc.service));
	    }
	}
	else {
	    (q(service), $rc, q(restart));
	}
    }
}

# map UID to username (cached)
my %uidcache;
sub uid2name($) {
    my $uid = shift;

    return $uidcache{$uid} if(exists($uidcache{$uid}));

    return $uidcache{$uid} = getpwuid($uid) || $uid;
}


my %nagios = (
    # kernel
    kstr => q(unknown),
    kret => 3,
    kperf => q(U),

    # services
    sstr => q(unknown),
    sret => 3,
    sperf => q(U),

    # sessions
    ustr => q(unknown),
    uret => 3,
    uperf => q(U),
 );
print "NEEDRESTART-VER: $NeedRestart::VERSION\n" if($opt_b && !$opt_p);

my %restart;
my %sessions;
my @easy_hints;
if(defined($opt_l)) {
    my @ign_pids=($$, getppid());

    # inspect only pids
    my $ptable = nr_ptable();

    # find session parent
    sub findppid($@) {
	my $uid = shift;
	my ($pid, @pids) = @_;
	
	if($ptable->{$pid}->{ppid} == 1) {
	    return $pid
		if($ptable->{$pid}->{uid} == $uid);
	    
	    return undef;
	}
	
	foreach my $pid (@pids) {
	    my $ppid = &findppid($uid, $pid);
	    
	    return $ppid if($ppid);
	}
	
	return $pid;
    }

    $ui->progress_prep(scalar keys %$ptable, __ 'Scanning processes...');
    my %stage2;
    for my $pid (sort {$a <=> $b} keys %$ptable) {
	$ui->progress_step;

	# user-mode: skip foreign processes
	next if($uid && $ptable->{$pid}->{uid} != $uid);

	# skip myself
	next if(grep {$pid == $_} @ign_pids);

	my $restart = 0;
	my $exe = nr_readlink($pid);

	# ignore kernel threads
	next unless(defined($exe));

	# orphaned binary
	$restart++ if (defined($exe) && $exe =~ s/ \(deleted\)$//);  # Linux
	$restart++ if (defined($exe) && $exe =~ s/^\(deleted\)//);   # Linux VServer
	print STDERR "$LOGPREF #$pid uses obsolete binary $exe\n" if($restart && $nrconf{verbosity} > 1);

	# ignore blacklisted binaries
	next if(grep { $exe =~ /$_/; } @{$nrconf{blacklist}});

	# read file mappings (Linux 2.0+)
	unless($restart) {
	    open(HMAP, '<', "/proc/$pid/maps") || next;
	    while(<HMAP>) {
		chomp;
		my ($maddr, $mperm, $moffset, $mdev, $minode, $path) = split(/\s+/, $_, 6);

		# skip special handles and non-executable mappings
		next unless(defined($path) && $minode != 0 && $path ne '' && $mperm =~ /x/);

		# skip special device paths
		next if($path =~ m@^/(SYSV00000000$|drm$|dev/)@);

		# skip aio(7) mapping
		next if($path =~ m@^/\[aio\]@);

		# skip Oil Runtime Compiler's JIT files
		next if($path =~ m@/orcexec\.[\w\d]+( \(deleted\))?$@);

		# removed executable mapped files
		if($path =~ s/ \(deleted\)$// ||  # Linux
		   $path =~ s/^\(deleted\)//) {   # Linux VServer
		    print STDERR "$LOGPREF #$pid uses deleted $path\n" if($nrconf{verbosity} > 1);
		    $restart++;
		    last;
		}

		# check for non-existing libs
		my @paths = ("/proc/$pid/map_files/$maddr", "/proc/$pid/root/$path");
		my ($testp) = grep { -e $_; } @paths;
		unless($testp) {
		    unless($path =~ m@^/tmp/@ || $path =~ m@^(/var)?/run/@) {
			print STDERR "$LOGPREF #$pid uses non-existing $path\n" if($nrconf{verbosity} > 1);
			$restart++;
			last;
		    }
		}

		# get on-disk info
		my ($sdev, $sinode) = stat($testp);
		my @sdevs = (
		    # glibc gnu_dev_* definition from sysmacros.h
		    sprintf("%02x:%02x", (($sdev >> 8) & 0xfff) | (($sdev >> 32) & ~0xfff), (($sdev & 0xff) | (($sdev >> 12) & ~0xff))),
		    # Traditional definition of major(3) and minor(3)
		    sprintf("%02x:%02x", $sdev >> 8, $sdev & 0xff),
		    # kFreeBSD: /proc/<pid>/maps does not contain device IDs
		    qq(00:00)
		    );

		# Don't compare device numbers on anon filesystems
		# w/o a backing device (like OpenVZ's simfs).
		$mdev = "00:00"
		    if (((($sdev >> 8) & 0xfff) |
			 (($sdev >> 32) & ~0xfff)) == 0);

		# compare maps content vs. on-disk
		unless($minode eq $sinode && ((grep {$mdev eq $_} @sdevs) ||
					      # BTRFS breaks device ID mapping completely...
					      # ignoring unnamed device IDs for now
					      $mdev =~ /^00:/)) {
		    print STDERR "$LOGPREF #$pid uses obsolete $path\n" if($nrconf{verbosity} > 1);
		    $restart++;
		    last;
		}
	    }
	    close(HMAP);
	}

	unless($restart || !$nrconf{interpscan}) {
	    $restart++ if(needrestart_interp_check($nrconf{verbosity} > 1, $pid, $exe));
	}

	# handle containers (LXC, docker, etc.)
	next if($restart && needrestart_cont_check($nrconf{verbosity} > 1, $pid, $exe));

	# restart needed?
	next unless($restart);

	# handle user sessions
	if($ptable->{$pid}->{ttydev} ne '' && !$is_systemd) {
	    my $ttydev = realpath( $ptable->{$pid}->{ttydev} );
	    print STDERR "$LOGPREF #$pid part of user session: uid=$ptable->{$pid}->{uid} sess=$ttydev\n" if($nrconf{verbosity} > 1);
	    push(@{ $sessions{ $ptable->{$pid}->{uid} }->{ $ttydev }->{ $ptable->{$pid}->{fname} } }, $pid);

	    # add session processes to stage2 only in user mode
	    $stage2{$pid} = $exe if($uid);

	    next;
	}

	# find parent process
	my $ppid = $ptable->{$pid}->{ppid};
	if($ppid != $pid && $ppid > 1 && !$uid) {
	    print STDERR "$LOGPREF #$pid is a child of #$ppid\n" if($nrconf{verbosity} > 1);

	    if($uid && $ptable->{$ppid}->{uid} != $uid) {
		print STDERR "$LOGPREF #$ppid is a foreign process\n" if($nrconf{verbosity} > 1);
		$stage2{$pid} = $exe;
	    }
	    else {
		unless(exists($stage2{$ppid})) {
		    my $pexe = nr_readlink($ppid);
		    # ignore kernel threads
		    next unless(defined($pexe));

		    $stage2{$ppid} = $pexe;
		}
	    }
	}
	else {
	    print STDERR "$LOGPREF #$pid is not a child\n" if($nrconf{verbosity} > 1 && !$uid);
	    $stage2{$pid} = $exe;
	}
    }
    $ui->progress_fin;

    if(scalar keys %stage2 && !$uid) {
	$ui->progress_prep(scalar keys %stage2, __ 'Scanning candidates...');
	foreach my $pid (sort {$a <=> $b} keys %stage2) {
	    $ui->progress_step;

	    # skip myself
	    next if(grep {$pid == $_} @ign_pids);

	    my $exe = nr_readlink($pid);
	    $exe =~ s/ \(deleted\)$//;  # Linux
	    $exe =~ s/^\(deleted\)//;   # Linux VServer
	    print STDERR "$LOGPREF #$pid exe => $exe\n" if($nrconf{verbosity} > 1);

	    # try to find interpreter source file
	    ($exe) = (needrestart_interp_source($nrconf{verbosity} > 1, $pid, $exe), $exe);

	    # ignore blacklisted binaries
	    next if(grep { $exe =~ /$_/; } @{$nrconf{blacklist}});

	    if($is_systemd) {
		# systemd manager
		if($pid == 1 && $exe =~ m@^/lib/systemd/systemd@) {
		    print STDERR "$LOGPREF #$pid is systemd manager\n" if($nrconf{verbosity} > 1);
		    $restart{q(systemd manager)}++;
		    next;
		}

		# get unit name from /proc/<pid>/cgroup
		if(open(HCGROUP, qq(/proc/$pid/cgroup))) {
		    my ($rc) = map {
			chomp;
			my ($id, $type, $value) = split(/:/);
			if($type ne q(name=systemd)) {
			    ();
			}
			else {
			    if($value =~ m@/user-(\d+)\.slice/session-(\d+)\.scope@) {
				print STDERR "$LOGPREF #$pid part of user session: uid=$1 sess=$2\n" if($nrconf{verbosity} > 1);
				push(@{ $sessions{$1}->{"session #$2"}->{ $ptable->{$pid}->{fname} } }, $pid);
				next;
			    }
			    if($value =~ m@/user\@(\d+)\.service@) {
				print STDERR "$LOGPREF #$pid part of user manager service: uid=$1\n" if($nrconf{verbosity} > 1);
				push(@{ $sessions{$1}->{'user manager service'}->{ $ptable->{$pid}->{fname} } }, $pid);
				next;
			    }
			    elsif($value =~ m@/([^/]+\.service)$@) {
				($1);
			    }
			    else {
				print STDERR "$LOGPREF #$pid unexpected cgroup '$value'\n" if($nrconf{verbosity} > 1);
				();
			    }
			}
		    } <HCGROUP>;
		    close(HCGROUP);

		    if($rc) {
			print STDERR "$LOGPREF #$pid is $rc\n" if($nrconf{verbosity} > 1);
			$restart{$rc}++;
			next;
		    }
		}

		# did not get the unit name, yet - try systemctl status
		print STDERR "$LOGPREF /proc/$pid/cgroup: $!\n" if($nrconf{verbosity} > 1 && $!);
		print STDERR "$LOGPREF trying systemctl status\n" if($nrconf{verbosity} > 1);
		my $systemctl = nr_fork_pipe($nrconf{verbosity} > 1, qq(systemctl), qq(-n), qq(0), qq(--full), qq(status), $pid);
		my $ret = <$systemctl>;
		close($systemctl);

		if(defined($ret) && $ret =~ /([^\s]+\.service)( |$)/) {
		    my $s = $1;
		    print STDERR "$LOGPREF #$pid is $s\n" if($nrconf{verbosity} > 1);
		    $restart{$s}++;
		    $s =~ s/\.service$//;
		    delete($restart{$s});
		    next;
		}
	    }
	    else {
		# sysv init
		if($pid == 1 && $exe =~ m@^/sbin/init@) {
		    print STDERR "$LOGPREF #$pid is sysv init\n" if($nrconf{verbosity} > 1);
		    $restart{q(sysv init)}++;
		    next;
		}
	    }

	    my $pkg;
	    foreach my $hook (nsort <$nrconf{hook_d}/*>) {
		print STDERR "$LOGPREF #$pid running $hook\n" if($nrconf{verbosity} > 1);

		my $found = 0;
		my $prun = nr_fork_pipe($nrconf{verbosity} > 1, $hook, ($nrconf{verbosity} > 1 ? qw(-v) : ()), $exe);
		my @nopids;
		while(<$prun>) {
		    chomp;
		    my @v = split(/\|/);

		    if($v[0] eq 'PACKAGE' && $v[1]) {
			$pkg = $v[1];
			print STDERR "$LOGPREF #$pid package: $v[1]\n" if($nrconf{verbosity} > 1);
			next;
		    }

		    if($v[0] eq 'RC') {
			my %lsb = parse_lsbinit($v[1]);

			unless(%lsb && exists($lsb{'default-start'})) {
			    # If the script has no LSB tags we consider to call it later - they
			    # are broken anyway.
			    print STDERR "$LOGPREF no LSB headers found at $v[1]\n" if($nrconf{verbosity} > 1);
			    push(@nopids, $v[1]);
			}
			# In the run-levels S and 1 no daemons are being started (normaly).
			# We don't call any rc.d script not started in the current run-level.
			elsif($lsb{'default-start'} =~ /$runlevel/) {
			    # If a pidfile has been found, try to look for the daemon and ignore
			    # any forked/detached childs (just a heuristic due Debian Bug#721810).
			    if(exists($lsb{pidfiles})) {
				foreach my $pidfile (@{ $lsb{pidfiles} }) {
				    open(HPID, '<', "$pidfile") || next;
				    my $p = <HPID>;
				    close(HPID);

				    if(int($p) == $pid) {
					print STDERR "$LOGPREF #$pid has been started by $v[1] - triggering\n" if($nrconf{verbosity} > 1);
					$restart{$v[1]}++;
					$found++;
					last;
				    }
				}
			    }
			    else {
				print STDERR "$LOGPREF no pidfile reference found at $v[1]\n" if($nrconf{verbosity} > 1);
				push(@nopids, $v[1]);
			    }
			}
			else {
			    print STDERR "$LOGPREF #$pid rc.d script $v[1] should not start in the current run-level($runlevel)\n" if($nrconf{verbosity} > 1);
			}
		    }
		}

		# No perfect hit - call any rc scripts instead.
		if(!$found && $#nopids > -1) {
		    foreach my $rc (@nopids) {
			if($is_systemd && exists($restart{"$rc.service"})) {
			    print STDERR "$LOGPREF #$pid rc.d script $rc seems to be superseeded by $rc.service\n" if($nrconf{verbosity} > 1);
			}
			else {
			    $restart{$rc}++;
			}
		    }
		    $found++;
		}

		last if($found);
	    }
	}
	$ui->progress_fin;
    }

    # List user's processes in user-mode
    if($uid && scalar %stage2) {
	my %fnames;
	foreach my $pid (keys %stage2) {
	    push(@{$fnames{ $ptable->{$pid}->{fname} }}, $pid);
	}

	if($opt_b) {
	    print map { "NEEDRESTART-PID: $_=".join(',', @{ $fnames{$_} })."\n"; } nsort keys %fnames;
	}
	else {
	    $ui->notice(__ 'Your outdated processes:');
	    $ui->notice(join(', ',map { $_.'['.join(', ', @{ $fnames{$_} }).']';  } nsort keys %fnames));
	}
    }
}

# Apply rc/service blacklist
foreach my $rc (keys %restart) {
    next unless(scalar grep { $rc =~ /$_/; } @{$nrconf{blacklist_rc}});

    print STDERR "$LOGPREF $rc is blacklisted -> ignored\n" if($nrconf{verbosity} > 1);
    delete($restart{$rc});
}

# Skip kernel stuff within containers
if(needrestart_cont_check($nrconf{verbosity} > 1, 1, nr_readlink(1), 1)) {
    print STDERR "$LOGPREF container detected, skipping kernel check\n" if($nrconf{verbosity} > 1);
    $opt_k = undef;
}

if(defined($opt_k)) {
    my ($kresult, %kvars) = ($nrconf{kernelhints} || $opt_b ? nr_kernel_check($nrconf{verbosity} > 1, $ui) : ());

    if(defined($kresult)) {
	if($opt_b) {
	    unless($opt_p) {
		print "NEEDRESTART-KCUR: $kvars{KVERSION}\n";
		print "NEEDRESTART-KEXP: $kvars{EVERSION}\n" if(defined($kvars{EVERSION}));
		print "NEEDRESTART-KSTA: $kresult\n";
	    }
	    else {
		$nagios{kstr} = $kvars{KVERSION};
		if($kresult == NRK_VERUPGRADE) {
		    $nagios{kstr} .= "!=$kvars{EVERSION} (!)";
		    $nagios{kret} = 1;
		    $nagios{kperf} = 2;
		}
		elsif($kresult == NRK_ABIUPGRADE) {
		    $nagios{kstr} .= " (!)";
		    $nagios{kret} = 1;
		    $nagios{kperf} = 1;
		}
		elsif($kresult == NRK_NOUPGRADE) {
		    $nagios{kret} = 0;
		    $nagios{kperf} = 0;
		}
	    }
	}
	else {
	    if($kresult == NRK_NOUPGRADE) {
		$ui->notice(($kvars{ABIDETECT} ? __('Running kernel seems to be up-to-date.') : __('Running kernel seems to be up-to-date (ABI upgrades are not detected).')))
		    unless($opt_m eq 'e');
	    }
	    elsif($kresult == NRK_ABIUPGRADE) {
		push(@easy_hints, __ 'an outdated kernel image') if($opt_m eq 'e');

		if($nrconf{kernelhints} < 0) {
		    $ui->notice(__x(
				    'The currently running kernel version is {kversion} and there is an ABI compatible upgrade pending.',
				    kversion => $kvars{KVERSION},
				));
		}
		else {
		    $ui->announce_abi(%kvars);
		}
	    }
	    elsif($kresult == NRK_VERUPGRADE) {
		push(@easy_hints, __ 'an outdated kernel image') if($opt_m eq 'e');

		if($nrconf{kernelhints} < 0) {
		    $ui->notice(__x(
				    'The currently running kernel version is {kversion} which is not the expected kernel version {eversion}.',
				    kversion => $kvars{KVERSION},
				    eversion => $kvars{EVERSION},
				));
		}
		else {
		    $ui->announce_ver(%kvars);
		}
	    }
	    else {
		$ui->notice(__ 'Failed to retrieve available kernel versions.');
	    }
	}
    }
}

if(defined($opt_l) && !$uid) {
    ## SERVICES
    unless(scalar %restart) {
	$ui->notice(__ 'No services need to be restarted.') unless($opt_b || $opt_m eq 'e');
	if($opt_p) {
	    $nagios{sstr} = q(none);
	    $nagios{sret} = 0;
	    $nagios{sperf} = 0;
	}
    }
    else {
	if($opt_m eq 'e' && $opt_r ne 'i') {
	    push(@easy_hints, __ 'outdated binaries');
	}
	elsif($opt_b || $opt_r ne 'i') {
	    my @skipped_services;

	    $ui->notice(__ 'Services to be restarted:') if($opt_r eq 'l');
	    $ui->notice(__ 'Restarting services...') if($opt_r eq 'a');
	    if($opt_p) {
		$nagios{sstr} = (scalar keys %restart).' (!)';
		$nagios{sret} = 2;
		$nagios{sperf} = (scalar keys %restart);
	    }
	    
	    foreach my $rc (sort { lc($a) cmp lc($b) } keys %restart) {
		# always combine restarts in one systemctl command
		local %nrconf;
		$nrconf{systemctl_combine} = 1 unless($opt_r eq 'l');

		if($opt_b) {
		    print "NEEDRESTART-SVC: $rc\n" unless($opt_p);
		    next;
		}

		# don't restart greylisted services...
		my $restart = !$nrconf{defno};
		foreach my $re (keys %{$nrconf{override_rc}}) {
		    next unless($rc =~ /$re/);

		    $restart = $nrconf{override_rc}->{$re};
		    last;
		}
		# ...but complain about them
		unless($restart) {
		    push(@skipped_services, $rc);
		    next;
		}

		my @cmd = restart_cmd($rc);
		next unless($#cmd > -1);

		$ui->notice(join(' ', '', @cmd));
		$ui->runcmd(sub {
		    system(@cmd) if($opt_r eq 'a');
			    });
	    }
	
	    unless($#systemd_restart == -1) {
		my @cmd = (qq(systemctl), qq(restart), @systemd_restart);
		$ui->notice(join(' ', '', @cmd));
	    }

	    @systemd_restart = ();
	    if($#skipped_services > -1) {
		$ui->notice(__ 'Services being skipped:');
		foreach my $rc (sort @skipped_services) {
		    my @cmd = restart_cmd($rc);
		    $ui->notice(join(' ', '', @cmd)) if($#cmd > -1);
		}

		unless($#systemd_restart == -1) {
		    my @cmd = (qq(systemctl), qq(restart), @systemd_restart);
		    $ui->notice(join(' ', '', @cmd));
		}
	    }
	}
	else {
	    my $o = 0;
	    my @skipped_services = keys %restart;

	    $ui->notice(__ 'Restarting services...');
	    $ui->query_pkgs(__('Services to be restarted:'), $nrconf{defno}, \%restart, $nrconf{override_rc},
			    sub {
				# always combine restarts in one systemctl command
				local %nrconf;
				$nrconf{systemctl_combine} = 1;

				my $rc = shift;
				@skipped_services = grep { $_ ne $rc; } @skipped_services;

				my @cmd = restart_cmd($rc);
				return unless($#cmd > -1);

				$ui->notice(join(' ', '', @cmd));
				system(@cmd);
			    });

	    if($#systemd_restart > -1) {
		my @cmd = (qw(systemctl restart), @systemd_restart);

		$ui->notice(join(' ', '', @cmd));
		$ui->runcmd(sub {
		    system(@cmd);
			    });
	    }

	    @systemd_restart = ();
	    if($#skipped_services > -1) {
		$ui->notice(__ 'Services being skipped:');
		foreach my $rc (sort @skipped_services) {
		    my @cmd = restart_cmd($rc);
		    $ui->notice(join(' ', '', @cmd)) if($#cmd > -1);
		}

		unless($#systemd_restart == -1) {
		    my @cmd = (qq(systemctl), qq(restart), @systemd_restart);
		    $ui->notice(join(' ', '', @cmd));
		}
	    }
	}
    }


    ## CONTAINERS
    @systemd_restart = ();
    my %conts = needrestart_cont_get($nrconf{verbosity} > 1);
    unless(scalar %conts) {
	$ui->notice(__ 'No containers need to be restarted.') unless($opt_b || $opt_m eq 'e');
	if($opt_p) {
	    $nagios{cstr} = q(none);
	    $nagios{cret} = 0;
	    $nagios{cperf} = 0;
	}
    }
    else {
	if($opt_m eq 'e' && $opt_r ne 'i') {
	    push(@easy_hints, __ 'outdated containers');
	}
	elsif($opt_b || $opt_r ne 'i') {
	    my @skipped_containers;

	    $ui->notice(__ 'Containers to be restarted:') if($opt_r eq 'l');
	    $ui->notice(__ 'Restarting containers...') if($opt_r eq 'a');
	    if($opt_p) {
		$nagios{cstr} = (scalar keys %conts).' (!)';
		$nagios{cret} = 2;
		$nagios{cperf} = (scalar keys %conts);
	    }

	    foreach my $cont (sort { lc($a) cmp lc($b) } keys %conts) {
		if($opt_b) {
		    print "NEEDRESTART-CONT: $cont\n" unless($opt_p);
		    next;
		}

		# don't restart greylisted containers...
		my $restart = !$nrconf{defno};
		foreach my $re (keys %{$nrconf{override_cont}}) {
		    next unless($cont =~ /$re/);

		    $restart = $nrconf{override_cont}->{$re};
		    last;
		}
		# ...but complain about them
		unless($restart) {
		    push(@skipped_containers, $cont);
		    next;
		}

		$ui->notice(join(' ', '', @{ $conts{$cont} }));
		$ui->runcmd(sub {
		    system(@{ $conts{$cont} }) if($opt_r eq 'a');
			    });
	    }

	    if($#skipped_containers > -1) {
		$ui->notice(__ 'Containers being skipped:');
		foreach my $cont (sort @skipped_containers) {
		    $ui->notice(join(' ', '', @{ $conts{$cont} }));
		}
	    }
	}
	else {
	    my $o = 0;

	    $ui->notice(__ 'Restarting containers...');
	    $ui->query_conts(__('Containers to be restarted:'), $nrconf{defno}, \%conts, $nrconf{override_rc},
			     sub {
				 my $cont = shift;
				 $ui->notice(join(' ', '', @{ $conts{$cont} }));
				 system(@{ $conts{$cont} });
			     });
	}
    }

    ## SESSIONS
    # list and notify user sessions
    unless(scalar keys %sessions) {
	$ui->notice(__ 'No user sessions are running outdated binaries.') unless($opt_b || $opt_m eq 'e');
	if($opt_p) {
	    $nagios{ustr} = 'none';
	    $nagios{uret} = 0;
	    $nagios{uperf} = 0;
	}
    }
    else {
	if($opt_m eq 'e') {
	    push(@easy_hints, __ 'outdated sessions');
	}
	else {
	    $ui->notice(__ 'User sessions running outdated binaries:');
	}
	if($opt_p) {
	    $nagios{ustr} = (scalar keys %sessions).' (!)';
	    $nagios{uret} = 1;
	    $nagios{uperf} = (scalar keys %sessions);
	}
	unless($opt_p || $opt_b) {
	    foreach my $uid (sort { ncmp(uid2name($a), uid2name($b)); } keys %sessions) {
		foreach my $sess (sort keys %{ $sessions{$uid} }) {
		    my $fnames = join(', ',map { $_.'['.join(',', @{ $sessions{$uid}->{$sess}->{$_} }).']';  } nsort keys %{ $sessions{$uid}->{$sess} });
		    $ui->notice(' '.uid2name($uid)." @ $sess: $fnames") unless($opt_m eq 'e');
		    if($nrconf{sendnotify}) {
			local %ENV;

			$ENV{NR_UID} = $uid;
			$ENV{NR_USERNAME} = uid2name($uid);
			$ENV{NR_SESSION} = $sess;
			$ENV{NR_SESSPPID} = findppid($uid, sort map { @$_; } values %{ $sessions{$uid}->{$sess} });

			foreach my $bin (nsort <$nrconf{notify_d}/*>) {
			    next unless(-x $bin);
			    next if($bin =~ /(~|\.dpkg-[^.]+)$/);

			    print STDERR "$LOGPREF run $bin\n" if($nrconf{verbosity} > 1);
			    my $pipe = nr_fork_pipew($nrconf{verbosity} > 1, $bin);
			    print $pipe "$fnames\n";
			    last if(close($pipe));
			}
		    }
		}
	    }
	}
    }
}

# easy mode: print hint on outdated stuff
if(scalar @easy_hints) {
    my $t = pop(@easy_hints);
    my $h = join(', ', @easy_hints);
    $ui->announce_ehint(EHINT => ($h ? join(' ', $h, __ 'and', '') : '') . $t);
}

# nagios plugin output
if($opt_p) {
    my %states = (
	0 => q(OK),
	1 => q(WARN),
	2 => q(CRIT),
	3 => q(UNKN),
	);
    my ($ret) = reverse sort 
	(($opt_k ? $nagios{kret} : ()),
	($opt_l ? ($nagios{sret}, $nagios{cret}, $nagios{uret}) : ()));

    print "$states{$ret} - ", join(', ',
	       ($opt_k ? "Kernel: $nagios{kstr}" : ()),
	       ($opt_l ? "Services: $nagios{sstr}" : ()),
	       ($opt_l ? "Containers: $nagios{cstr}" : ()),
	       ($opt_l ? "Sessions: $nagios{ustr}" : ()),
	), '|', join(' ',
	       ($opt_k ? "Kernel=$nagios{kperf};0;;0;2" : ()),
	       ($opt_l ? "Services=$nagios{sperf};;0;0" : ()),
	       ($opt_l ? "Containers=$nagios{cperf};;0;0" : ()),
	       ($opt_l ? "Sessions=$nagios{uperf};0;;0" : ()),
	), "\n";

    exit $ret;
}
